model record (
	key encryption_key_hash

	// record data
	field encryption_key_hash blob
	field created_at          timestamp
	field public              bool

	// denormalized information from access grant
	field satellite_address text
	field macaroon_head     blob
	field expires_at        timestamp ( nullable )

	// sensitive data
	field encrypted_secret_key   blob
	field encrypted_access_grant blob

	// invalid tracking
	field invalid_reason text      ( nullable, updatable )
	field invalid_at     timestamp ( nullable, updatable )
)

create record ( noreturn )

delete record (
	where record.encryption_key_hash = ?
)

read scalar (
	select record
	where record.encryption_key_hash = ?
)

read count (
	select record
)

read paged (
	select record
)

update record (
	noreturn
	where record.encryption_key_hash = ?
	where record.invalid_reason = null
)
